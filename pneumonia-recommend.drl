rule "PNEUMONIA Recommendation"
  dialect "mvel"
  enabled true
  when
    FhirPatient(ageInYears > 19 )
  then
    drools.setFocus("PNEUMONIA_RECOMMENDATION") ;
end

rule "PNEUMONIA RECOMMENDATION RULE 1 IMMUNOCOMPROMISING_CRITERIA"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 1000

  when
    $patient : Patient()

    // IMMUNE ISSUES
    // Immunocompromising Conditions
    // Congenital or acquired immunodeficiency including

    // SURVEY RESPONSES
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Did NOT answer YES to "Do you have any of these immunocompromising conditions for pneumonia conditions ...?"
    Boolean(booleanValue == false) from codedAnswerEquals($response, "PVAC-1-IMMU", "http://regenstrief.org/CDS/code/yesno", "1")

    List( size == 0 ) from collect( FhirResultObject() from getResult( patientRegistrations,
    [ "http://hl7.org/fhir/sid/icd-9-cm|279",
      //  Disorders involving the immune mechanism
      //  Deficiency of humoral immunity
      "http://hl7.org/fhir/sid/icd-9-cm|279.0",
      //  Deficiency of cell-mediated immunity
      "http://hl7.org/fhir/sid/icd-9-cm|279.1",
      //  Combined immunity deficiency
      "http://hl7.org/fhir/sid/icd-9-cm|279.2",
      //  Other specified disorders involving the immune mechanism
      "http://hl7.org/fhir/sid/icd-9-cm|279.8",
      //  Unspecified disorder of immune mechanism
      "http://hl7.org/fhir/sid/icd-9-cm|279.9",
      //  Immunodeficiency unspecified
      "http://hl7.org/fhir/sid/icd-10|D84.9",
      //  Immunodeficiency with predominantly antibody defects
      "http://hl7.org/fhir/sid/icd-10|D80",
      //  Combined immunodeficiencies
      "http://hl7.org/fhir/sid/icd-10|D81",
      //  Human immunodeficiency virus infection (disorder)
      "http://snomed.info/sct|86406008",
      // B- or T-lymphocyte deficiency
      //  Other specified disorders involving the immune mechanism
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-9-cm|279.8",
      //  Other disorders of white blood cells
      "http://hl7.org/fhir/sid/icd-10|D72",
      //  Other specified disorders of white blood cells
      "http://hl7.org/fhir/sid/icd-10|D72.8",
      //  Lymphocytopenia
      "http://hl7.org/fhir/sid/icd-10|D72.810",
      //  Other specified disorders of white blood cells
      "http://hl7.org/fhir/sid/icd-10|D72.89",
      //  Disorder of white blood cells unspecified
      "http://hl7.org/fhir/sid/icd-10|D72.9",
      //   Cellular immune defect (finding)
      "http://snomed.info/sct|398293003",
      // Complement deficiencies
      //  Other specified disorders involving the immune mechanism
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-9-cm|279.8",
      //  Complement deficiency
      "http://hl7.org/fhir/sid/icd-10|D84.1",
      //  Complement deficiency disease (disorder)
      "http://snomed.info/sct|24743004",
      // Phagocytic disorders (excluding chronic granulomatous disease)
      //  Unspecified diseases of blood and blood-forming organs
      "http://hl7.org/fhir/sid/icd-9-cm|289.9",
      //  Functional disorders of polymorphonuclear neutrophils
      "http://hl7.org/fhir/sid/icd-10|D71",
      //   Immunodeficiency disorder (disorder)
      "http://snomed.info/sct|234532001",
      // HIV infection
      //  Human immunodeficiency virus HIV disease
      "http://hl7.org/fhir/sid/icd-9-cm|042",
      //  human immunodeficiency virus HIV disease
      "http://hl7.org/fhir/sid/icd-10|B20",
      //  Human immunodeficiency virus positive (finding)
      "http://snomed.info/sct|165816005",
      // Chronic renal failure and nephrotic syndrome
      //  Chronic kidney disease unspecified
      "http://hl7.org/fhir/sid/icd-9-cm|585.9",
      //  Chronic kidney disease unspecified
      "http://hl7.org/fhir/sid/icd-10|N18.9",
      //   Chronic kidney disease stage 5 (disorder)
      "http://snomed.info/sct|433146000",
      // Leukemia
      //  Unspecified leukemia
      "http://hl7.org/fhir/sid/icd-9-cm|208.9",
      //  Lymphoid leukemia
      "http://hl7.org/fhir/sid/icd-10|C91",
      //  Myeloid leukemia
      "http://hl7.org/fhir/sid/icd-10|C92",
      //  Monocytic leukemia
      "http://hl7.org/fhir/sid/icd-10|C93",
      //  Other leukemias of specified cell type
      "http://hl7.org/fhir/sid/icd-10|C94",
      //  Leukemia of unspecified cell type
      "http://hl7.org/fhir/sid/icd-10|C95",
      //  Chronic myeloid leukemia disease (disorder)
      "http://snomed.info/sct|92818009",
      // lymphoma
      //  Other malignant lymphomas unspecified site extranodal and solid organ sites
      "http://hl7.org/fhir/sid/icd-9-cm|202.80",
      //  Follicular lymphoma
      "http://hl7.org/fhir/sid/icd-10|C82",
      //  Non-follicular lymphoma
      "http://hl7.org/fhir/sid/icd-10|C83",
      //  Mature T/NK-cell lymphomas
      "http://hl7.org/fhir/sid/icd-10|C84",
      //  Oth5er specified and unspecified types of non-Hodgkin lymphoma
      "http://hl7.org/fhir/sid/icd-10|C85",
      //  Other specified types of T/NK-cell lymphoma
      "http://hl7.org/fhir/sid/icd-10|C86",
      //  Malignant immunoproliferative diseases and certain other B-cell lymphomas
      "http://hl7.org/fhir/sid/icd-10|C88",
      //  Non-Hodgkins lymphoma (disorder)
      "http://snomed.info/sct|118601006",
      // Hodgkin disease
      //  Hodgkins disease unspecified type
      "http://hl7.org/fhir/sid/icd-9-cm|201.9",
      //  Hodgkin lymphoma
      "http://hl7.org/fhir/sid/icd-10|C81",
      //  Hodgkins disease mixed cellularity (disorder)
      "http://snomed.info/sct|118609008",
      // generalized malignancy
      //  Disseminated malignant neoplasm without specification of site
      "http://hl7.org/fhir/sid/icd-9-cm|199.0",
      //   Other and unspecified malignant neoplasms of lymphoid hematopoietic and related tissue
      "http://hl7.org/fhir/sid/icd-10|C96",
      //   Widespread metastatic malignant neoplastic disease (disorder)
      "http://snomed.info/sct|405843009",
      // multiple myeloma
      //  Multiple myeloma
      "http://hl7.org/fhir/sid/icd-9-cm|203.0",
      //  Multiple myeloma and malignant plasma cell neoplasms
      "http://hl7.org/fhir/sid/icd-10|C90",
      //   Multiple myeloma (disorder)
      "http://snomed.info/sct|109989006",
      // Solid organ transplant
      //  Organ or tissue replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42",
      //  Kidney replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.0",
      //  Heart replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.1",
      //  Heart valve replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.2",
      //  Skin replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.3",
      //  Bone replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.4",
      //  Cornea replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.5",
      //  Lung replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.6",
      //  Liver replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.7",
      //  Other specified organ or tissue replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.8",
      //  Bone marrow replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.81",
      //  Peripheral stem cells replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.82",
      //  Pancreas replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.83",
      //  Organ or tissue replaced by transplant intestines
      "http://hl7.org/fhir/sid/icd-9-cm|V42.84",
      //  Other specified organ or tissue replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.89",
      //  Unspecified organ or tissue replaced by transplant
      "http://hl7.org/fhir/sid/icd-9-cm|V42.9",
      //  Transplanted organ and tissue status
      "http://hl7.org/fhir/sid/icd-10|Z94",
      //  Kidney transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.0",
      //  Heart transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.1",
      //  Lung transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.2",
      //  Heart and lungs transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.3",
      //  Liver transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.4",
      //  Skin transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.5",
      //  Bone transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.6",
      //  Corneal transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.7",
      //  Other transplanted organ and tissue status
      "http://hl7.org/fhir/sid/icd-10|Z94.8",
      //  Bone marrow transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.81",
      //  Intestine transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.82",
      //  Pancreas transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.83",
      //  Stem cells transplant status
      "http://hl7.org/fhir/sid/icd-10|Z94.84",
      //  Other transplanted organ and tissue status
      "http://hl7.org/fhir/sid/icd-10|Z94.89",
      //  Transplanted organ and tissue status unspecified
      "http://hl7.org/fhir/sid/icd-10|Z94.9"], null, null, ResultTypeEnum.CONDITION, "ANY") );

    //   Solid organ transplant (procedure)
    List( size == 0 ) from collect( FhirResultObject() from getResult( patientRegistrations,
    [ "http://snomed.info/sct|313039003",
      //   Cadaveric renal transplant (procedure)
      "http://snomed.info/sct|175902000",
      //   Donor renal transplantation (procedure)
      "http://snomed.info/sct|313030004",
      //   Transplant of kidney (procedure)
      "http://snomed.info/sct|70536003",
      //  Transplantation of heart (procedure)
      "http://snomed.info/sct|32413006",
      //   Transplantation of liver (procedure)
      "http://snomed.info/sct|18027006",
      //   Transplantation of pancreas (procedure)
      "http://snomed.info/sct|62438007",
      //  Transplantation of spleen (procedure)
      "http://snomed.info/sct|61535006"], null, null, ResultTypeEnum.PROCEDURE, "ANY") );

    // Iatrogenic immunosuppression including long-term systemic corticosteroid and radiation therapy.
    //  Personal history of immunosuppressive therapy
    List( size == 0 ) from collect( FhirResultObject() from getResult( patientRegistrations,
    [ "http://hl7.org/fhir/sid/icd-9-cm|V87.46",
      //  Personal history of systemic steroid therapy
      "http://hl7.org/fhir/sid/icd-9-cm|V87.45",
      //  Personal history of immunosupression therapy
      "http://hl7.org/fhir/sid/icd-10|Z92.25",
      //  Personal history of systemic steroid therapy
      "http://hl7.org/fhir/sid/icd-10|Z92.241",
      //  Long term systemic steroid user (finding)
      "http://snomed.info/sct|12240661000119103"], null, null, ResultTypeEnum.CONDITION, "ANY") );

    //   Radiation therapy procedure or service (procedure)
    List( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|53438000", null, null, ResultTypeEnum.PROCEDURE, "ANY") )

    // Anatomical or functional asplenia including
    //  asplenia
    List( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations,
    [ "http://hl7.org/fhir/sid/icd-9-cm|759.0",
      //  Acquired absence of spleen
      "http://hl7.org/fhir/sid/icd-10|Z90.81",
      //  Disorder of spleen (disorder)
      "http://snomed.info/sct|51244008",
      // Sickle cell disease or other hemoglobinopathies
      //  Sickle-cell disease unspecified
      "http://hl7.org/fhir/sid/icd-9-cm|282.60",
      //  Sickle-cell disorders
      "http://hl7.org/fhir/sid/icd-10|D57",
      //  Sickle cell trait (disorder)
      "http://snomed.info/sct|16402000",
      // Congenital or acquired asplenia
      //  Other acquired absence of organ
      "http://hl7.org/fhir/sid/icd-9-cm|V45.79",
      //  Acquired absence of spleen
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-10|Z90.81",
      //  Asplenia (congenital)
      "http://hl7.org/fhir/sid/icd-10|Q89.01",
      //  Asplenia (disorder)
      "http://snomed.info/sct|707147002",
      //  Asplenia following surgical procedure (disorder)
      "http://snomed.info/sct|724639003",
      //  Congenital absence of spleen (disorder)
      "http://snomed.info/sct|93030006",
      // Splenic dysfunction
      //  Disease of spleen unspecified
      "http://hl7.org/fhir/sid/icd-9-cm|289.50",
      //  Disease of spleen unspecified
      "http://hl7.org/fhir/sid/icd-10|D73.9"], null, null, ResultTypeEnum.CONDITION, "ANY") );

      // BELOW COMMENTED OUT BECAUSE ALREADY SPECIFIED ABOVE
      //  Disorder of spleen (disorder)
      // DUPLICATED ABOVE
      // "http://snomed.info/sct|51244008"), null, null, ResultTypeEnum.CONDITION, "ANY") )
      // Splenectomy
      //  : Other acquired absence of organ
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-9-cm|V45.79"), null, null, ResultTypeEnum.CONDITION, "ANY") )
      //  Acquired absence of spleen
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-10|Z90.81"

    //  Splenectomy (procedure)
    List( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|234319005", null, null, ResultTypeEnum.PROCEDURE, "ANY") );

  then
    insert ( new String ( "PVAC_NOT_IMMUNOCOMPROMISED" ) );

end

rule "PNEUMONIA RECOMMENDATION RULE 2 HIGH_RISK_CRITERIA"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 900

  when
    $patient : Patient()

    // Pneumonia High Risk Conditions

    // SURVEY RESPONSES
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Did NOT answer YES to "Do you have any of these high risk for pneumonia conditions?"
    Boolean(booleanValue == false) from codedAnswerEquals($response, "PVAC-1-HR", "http://regenstrief.org/CDS/code/yesno", "1")

    // Congestive heart failure
    List( size == 0 ) from collect( FhirResultObject() from getResult( patientRegistrations,
    [ "http://hl7.org/fhir/sid/icd-9-cm|428.0",
      // Congestive heart failureunspecified
      //  Congestive heart failureunspecified
      "http://hl7.org/fhir/sid/icd-10|I50.9",
      //   Congestive heart failure (disorder)
      "http://snomed.info/sct|42343007",
      // Chronic congestive heart failure (disorder)  
      "http://snomed.info/sct|88805009",  
      //  Emphysema
      "http://hl7.org/fhir/sid/icd-9-cm|492.8",
      //  Emphysema
      "http://hl7.org/fhir/sid/icd-10|J43",
      //   Chronic emphysema (morphologic abnormality)
      "http://snomed.info/sct|125295001",
      // Chronic obstructive lung disease (disorder)
      "http://snomed.info/sct|13645005",
      // Chronic obstructive bronchitis (disorder)
      "http://snomed.info/sct|185086009",
      // Pulmonary emphysema (disorder)
      "http://snomed.info/sct|87433001",
      // asthma
      //  Extrinsic Asthma
      "http://hl7.org/fhir/sid/icd-9-cm|493.0",
      //  Intrinsic Asthma
      "http://hl7.org/fhir/sid/icd-9-cm|493.1",
      //  Chronic Obstructive Asthma
      "http://hl7.org/fhir/sid/icd-9-cm|493.2",
      //   Other forms of asthma
      "http://hl7.org/fhir/sid/icd-9-cm|493.8",
      //  Asthma unspecified type
      "http://hl7.org/fhir/sid/icd-9-cm|493.9",
      //  Asthma
      "http://hl7.org/fhir/sid/icd-10|J45",
      //   Asthma (disorder)
      "http://snomed.info/sct|195967001",
      // chronic lung disease
      //  Other pulmonary insufficiency not elsewhere classified
      "http://hl7.org/fhir/sid/icd-9-cm|518.82",
      //  Chronic respiratory failure
      "http://hl7.org/fhir/sid/icd-9-cm|518.83",
      //  Other diseases of lung not elsewhere classified
      "http://hl7.org/fhir/sid/icd-9-cm|518.89",
      //  Other chronic respiratory diseases originating in the perinatal period
      "http://hl7.org/fhir/sid/icd-10|P27.8",
      //  Other disorders of lung
      "http://hl7.org/fhir/sid/icd-10|J98.4",
      //  Chronic obstructive lung disease (disorder)
      "http://snomed.info/sct|13645005",
      //  Chronic lung disease (disorder)
      "http://snomed.info/sct|413839001",
      // Chronic liver disease
      //  Chronic liver disease and cirrhosis
      "http://hl7.org/fhir/sid/icd-9-cm|571",
      //  Other diseases of liver
      "http://hl7.org/fhir/sid/icd-10|K76",
      //    Chronic liver disease (disorder)
      "http://snomed.info/sct|328383001",
      // cirrhosis
      //  Chronic liver disease and cirrhosis
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-9-cm|571",
      //  Cirrhosis of liver without mention of alcohol
      "http://hl7.org/fhir/sid/icd-9-cm|571.5",
      //  Unspecified cirrhosis of liver
      "http://hl7.org/fhir/sid/icd-10|K74.60",
      //   Cirrhosis of liver (disorder)
      "http://snomed.info/sct|19943007",
      // Alcoholism
      //  Alcohol dependence syndrome
      "http://hl7.org/fhir/sid/icd-9-cm|303",
      //  Other and unspecified alcohol dependence
      "http://hl7.org/fhir/sid/icd-9-cm|303.9",
      //  Alchol dependence
      "http://hl7.org/fhir/sid/icd-10|F10.2",
      //  Finding relating to alcohol drinking behavior (finding)
      "http://snomed.info/sct|228273003",
      // Diabetes mellitus
      "http://hl7.org/fhir/sid/icd-9-cm|571.5",
      // DUPLICATED ABOVE
      // "http://hl7.org/fhir/sid/icd-10|K74.60",
      //  Diabetes mellitus (disorder)
      "http://snomed.info/sct|73211009",
      // Smoke cigarettes
      //  Personal history of tobacco use
      "http://hl7.org/fhir/sid/icd-9-cm|V15.82",
      //  Personal history of nicotine dependence
      "http://hl7.org/fhir/sid/icd-10|Z87.891",
      //   Smoker (finding)
      "http://snomed.info/sct|77176002"], null, null, ResultTypeEnum.CONDITION, "ANY") );

  then
    insert ( new String ( "PVAC_NOT_HIGH_RISK" ) );

end

/*
=============================================================

Summary of pneumonia recommendation logic:

For older than 65, give PCV13 if:
  * No h/o PCV13
  * No or unsure if they've had PCV13
  * No PPSV23 within 1 year

For older than 65 not immunocompromised, give PPSV23 if:
  * No PPSV23 after 65yo (or pt unsure)
  * No PPSV23 within 5 years
  * (PCV13 at least 1 year ago OR patient says they've gotten PCV13)

For immunocompromised (any age), give PPSV23 if:
  * No PPSV23 within 5 years
  * (PCV13 at least 8 weeks ago OR patient says they've gotten PCV13)

For under 65 and immunocompromised, give PCV13 if:
  * No PCV13
  * No or unsure if they've gotten PCV13

For under 65 high risk & not immunocompromised, give PPSV23 if:
  * No PPSV23
  * No or unsure if they've gotten PPSV23

=============================================================
*/

// ----------------------------------------------------------
// 65 OR OLDER
// ----------------------------------------------------------

rule "PNEUMONIA RECOMMENDATION RULE 3 65 OR OLDER "
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 600

  when
    $patient : Patient()
    FhirPatient(ageInYears >= 65 )

    // No history of PCV13
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90670", null, null, ResultTypeEnum.IMMUNIZATION, "ANY") )
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90670", null, null, ResultTypeEnum.PROCEDURE, "ANY") )
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", null, null, ResultTypeEnum.IMMUNIZATION, "ANY") );
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", null, null, ResultTypeEnum.PROCEDURE, "ANY") );


    // Survey Responses
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Answered NO or UNSURE to "Have you had the PCV13 Pneumonia Vaccine?"
    $pvac1pcv13 : String(this in ("2", "3")) from getAnswerValueString($response, "PVAC-1.PCV13") 

    // No PPSV23 within past year (Must wait a year before giving PCV13 after PPSV23)
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))          
  then
    // Recommend PCV13
    List cards = new ArrayList();

    String ind = IndicatorEnum.INFO.name();

    String txt = " Check with your doctor. All adults 65 and over should have the 13-valent pneumoccal conjugate vaccine (PCV13).";

    if ("2".equals($pvac1pcv13)) {
      ind = IndicatorEnum.WARNING.name();
      txt = "All adults 65 and over should have the 13-valent pneumoccal conjugate vaccine (PCV13).";
    }

    cards.add(createImmunizationRecommendation(
      ind,
      "Vaccinations",
      "One time",
      txt,
      "http://www.ama-assn.org/go/cpt",
      "90670",
      "PCV13 Pneumonia Vaccine" ,
      0));

    DroolsRuleResponse resp1 = new DroolsRuleResponse("message" ,"882", "RECOMMENDATION");
    resp1.putValue("CARDS", cards);
    insert(resp1);

end

// ----------------------------------------------------------
// 65 OR OLDER NOT IMMUNOCOMPROMISED 
// ----------------------------------------------------------

rule "PNEUMONIA RECOMMENDATION RULE 4 65 OR OLDER NOT IMMUNOCOMPROMISED"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 600

  when
    $patient : Patient()
    FhirPatient(ageInYears >= 65 )

    // Survey Responses
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Response to "Have you had the PCV13 Pneumonia Vaccine?"
    $pvac1pcv13 : String() from getAnswerValueString($response, "PVAC-1.PCV13")

    // NOT immunocompromised
    exists ( String ( toString() == "PVAC_NOT_IMMUNOCOMPROMISED" ) )

    // No PPSV23 after 65 years old
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getRepeatedEventJoda(65, $patient.getBirthDate()), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getRepeatedEventJoda(65, $patient.getBirthDate()), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getRepeatedEventJoda(65, $patient.getBirthDate()), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getRepeatedEventJoda(65, $patient.getBirthDate()), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))          

    // Responded NO or UNSURE to "Have you had the PPSV23 Pneumonia Vaccine since turning 65?"
    $pvac1ppsv23since65yr : String(this in ("2", "3")) from getAnswerValueString($response, "PVAC-1.PPSV23.SINCE.65YR") 

    // NO PPSV23 in last 5 years
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(1825), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(1825), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(1825), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
    List(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(1825), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))          

    // PCV13 a year or more ago OR patient says they've gotten PCV13
    exists(
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90670"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90670"), getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY") );
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", getWithinLastDayJoda(365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY") );
      or
      String(this in ("1", "3")) from $pvac1pcv13
    )

  then
    // Recommend PPSV23
    List cards = new ArrayList();

    String ind = IndicatorEnum.INFO.name();

    String txt = "Check with your doctor. All adults 65 and over should have the 23-valent pneumococcal vaccine (PPSV23) at least 1 year after the 13-valent pneumoccal conjugate vaccine (PCV13).";

    if ( $pvac1ppsv23since65yr == null || "2".equals($pvac1ppsv23since65yr) ) {
      ind = IndicatorEnum.WARNING.name();
      txt = "All adults 65 and over should have the 23-valent pneumococcal vaccine (PPSV23) at least 1 year after the 13-valent pneumoccal conjugate vaccine (PCV13).";
    }

    cards.add(createImmunizationRecommendation(
      ind,
      "Vaccinations",
      "One time",
      txt,
      "http://www.ama-assn.org/go/cpt",
      "90732",
      "PPSV23 Pneumonia Vaccine" ,
      0));

    DroolsRuleResponse resp1 = new DroolsRuleResponse("message" ,"882", "RECOMMENDATION");
    resp1.putValue("CARDS", cards);
    insert(resp1);

end

// ----------------------------------------------------------
// IMMUNOCOMPROMISED AT ANY AGE
// ----------------------------------------------------------

rule "PNEUMONIA RECOMMENDATION RULE 5 IMMUNOCOMPROMISED"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 600

  when
    $patient : Patient()

    // Survey Responses
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Response to "Have you had the PCV13 Pneumonia Vaccine?"
    $pvac1pcv13 : String() from getAnswerValueString($response, "PVAC-1.PCV13") 

    // Immunocompromised
    not( exists ( String ( toString() == "PVAC_NOT_IMMUNOCOMPROMISED" ) ) )

    // NO PPSV23 in last 5 years
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(5*365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90732"), getWithinLastDayJoda(5*365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(5*365), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://snomed.info/sct", "571631000119106"), getWithinLastDayJoda(5*365), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))          
    $pvac1ppsv23within5yr : String(this in ("2", "3")) from getAnswerValueString($response, "PVAC-1.PPSV23.WITHIN.5YR")

    // PCV13 more than 8 weeks ago OR patient says they've gotten PCV13
    exists(
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90670"), getWithinLastDayJoda(56), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY"))  
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, buildKeyFromConcept( "http://www.ama-assn.org/go/cpt", "90670"), getWithinLastDayJoda(56), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY"))  
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", getWithinLastDayJoda(56), getWithinLastDayJoda(0), ResultTypeEnum.IMMUNIZATION, "ANY") );
      or
      List(size > 0) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", getWithinLastDayJoda(56), getWithinLastDayJoda(0), ResultTypeEnum.PROCEDURE, "ANY") );
      or
      String(this in ("1")) from $pvac1pcv13
    )

  then
    // Recommend PPSV23
    List cards = new ArrayList();

    String ind = IndicatorEnum.INFO.name();

    String txt = "Check with your doctor. Immunocompromised adults should have the 23-valent pneumococcal vaccine (PPSV23) at least 8 weeks after the 13-valent pneumoccal conjugate vaccine (PCV13).";

    if (
        $pvac1ppsv23within5yr == null || "2".equals($pvac1ppsv23within5yr)
      ) {
      ind = IndicatorEnum.WARNING.name();
      txt = "Immunocompromised adults should have the 23-valent pneumococcal vaccine (PPSV23) at least 8 weeks after the 13-valent pneumoccal conjugate vaccine (PCV13).";
    }

    cards.add(createImmunizationRecommendation(
      ind,
      "Vaccinations",
      "One time",
      txt,
      "http://www.ama-assn.org/go/cpt",
      "90732",
      "PPSV23 Pneumonia Vaccine" ,
      0));

    DroolsRuleResponse resp1 = new DroolsRuleResponse("message" ,"882", "RECOMMENDATION");
    resp1.putValue("CARDS", cards);
    insert(resp1);

end

// ----------------------------------------------------------
// UNDER 65 AND IMMUNOCOMPROMISED
// ----------------------------------------------------------

rule "PNEUMONIA RECOMMENDATION RULE 6 UNDER 65 AND IMMUNOCOMPROMISED"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 600

  when
    $patient : Patient()
    FhirPatient(ageInYears < 65 )

    // Immunocompromised
    not( exists ( String ( toString() == "PVAC_NOT_IMMUNOCOMPROMISED" ) ) )

    // No history of PCV13
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90670", null, null, ResultTypeEnum.IMMUNIZATION, "ANY") );
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90670", null, null, ResultTypeEnum.PROCEDURE, "ANY") );
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", null, null, ResultTypeEnum.IMMUNIZATION, "ANY") );
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|459981000124107", null, null, ResultTypeEnum.PROCEDURE, "ANY") );

    // Survey Responses
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Answered NO or UNSURE to "Have you had the PCV13 Pneumonia Vaccine?"
    $pvac1pcv13 : String(this in ("2", "3") ) from getAnswerValueString($response, "PVAC-1.PCV13") 

  then
    // Recommend PCV13
    List cards = new ArrayList();

    String ind = IndicatorEnum.INFO.name();

    String txt = "Check with your doctor. Immunocompromised adults should have the 13-valent pneumoccal conjugate vaccine (PCV13).";

    if ( "2".equals($pvac1pcv13) ) {
      ind = IndicatorEnum.WARNING.name();
      txt = "Immunocompromised adults should have the 13-valent pneumoccal conjugate vaccine (PCV13).";
    }

    cards.add(createImmunizationRecommendation(
      ind,
      "Vaccinations",
      "One time",
      txt,
      "http://www.ama-assn.org/go/cpt",
      "90732",
      "PCV13 Pneumonia Vaccine" ,
      0));

    DroolsRuleResponse resp1 = new DroolsRuleResponse("message" ,"882", "RECOMMENDATION");
    resp1.putValue("CARDS", cards);
    insert(resp1);

end

// ----------------------------------------------------------
// UNDER 65 AND HIGH RISK (NOT IMMUNOCOMPROMISED)
// ----------------------------------------------------------

rule "PNEUMONIA RECOMMENDATION RULE 7 UNDER 65 AND HIGH RISK"
  dialect "mvel"
  enabled true
  agenda-group "PNEUMONIA_RECOMMENDATION"
  salience 600

  when
    $patient : Patient()
    FhirPatient(ageInYears < 65 )

    // High Risk
    not ( exists ( String ( toString() == "PVAC_NOT_HIGH_RISK" ) ) )

    // NOT Immunocompromised
    exists ( String ( toString() == "PVAC_NOT_IMMUNOCOMPROMISED" ) )

    // No history of PPSV23
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90732", null, null, ResultTypeEnum.IMMUNIZATION, "ANY") );
    ArrayList( size == 0 ) from collect( FhirResultObject() from getResult(patientRegistrations, "http://www.ama-assn.org/go/cpt|90732", null, null, ResultTypeEnum.PROCEDURE, "ANY") );
    ArrayList( size == 0 ) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|571631000119106", null, null, ResultTypeEnum.IMMUNIZATION, "ANY"))  
    ArrayList(size == 0) from collect(FhirResultObject() from getResult(patientRegistrations, "http://snomed.info/sct|571631000119106", null, null, ResultTypeEnum.PROCEDURE, "ANY"))          

    // Survey Responses
    $requestContext: CDSRequestContext()
    $response: QuestionnaireResponse() from $requestContext.get("questionnaireResponse")

    // Answered NO or UNSURE to "Have you gotten the PPSV23 pneumonia vaccine?"
    $pvac1pcv13 : String(this in ("2", "3") ) from getAnswerValueString($response, "PVAC-1.PPSV23") 

  then
    // Recommend PPSV23
    List cards = new ArrayList();

    String ind = IndicatorEnum.INFO.name();

    String txt = "Check with your doctor. Adults with certain high risk conditions should have the 23-valent pneumococcal vaccine (PPSV23).";

    if ( "2".equals($pvac1pcv13) ) {
      ind = IndicatorEnum.WARNING.name();
      txt = "Adults with certain high risk conditions should have the 23-valent pneumococcal vaccine (PPSV23).";
    }

    cards.add(createImmunizationRecommendation(
      ind,
      "Vaccinations",
      "One time",
      txt,
      "http://www.ama-assn.org/go/cpt",
      "90732",
      "PPSV23 Pneumonia Vaccine" ,
      0));

    DroolsRuleResponse resp1 = new DroolsRuleResponse("message" ,"882", "RECOMMENDATION");
    resp1.putValue("CARDS", cards);
    insert(resp1);

end